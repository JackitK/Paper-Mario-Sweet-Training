% Special script for giving a player items if they run out after loosing to a fight in the dojo.
    % This is is made to assure fights are possible to beat even if they waste all their items early on.
    % Therefore it should be used sparely to assure it doesn't get abused to make fights too easy.


#export:Script $PityGift_Dojo
{
    %If in an "no item mode", end the script early. (Used the no gift item flag for now since no item modes trigger it)
    If *GF_MAC00_StopGifts == .True
        Return
    EndIf

    % Check to see if the player is completly out of items
    Call $checkTotalItemsInInventory(*Var0)
    If (*Var0 == 0)

        % Run a check to see if Ginger T is awaiting to give Mario items. Don't want to be redundant on Tea Item gifts  
            % Run through this check; Setting *Var1 to .True if safe to give tea & to .False if there is still tea to claim.
            % Play a message to tell the player to go check out Ginger T for addtional items if relivant
        Set *Var1 .False
        Switch *GB_MAC00_TeaClaims
        % If no rewards are claimed...
        Case < 1
            % Check if the first two battles have been cleared....
            If *GB_MAC00_DojoRank  >  00000001 % Rank 2 or higher
                %Tea Reward hasn't been claimed yet; suddly remind the player
                % This will get called when actually giving the items...
            Else
                % First Ginger T requirement hasn't been met: Safe to give tea items 
                Set *Var1 .True
            EndIf
        % After first reward...
        Case < 2 
            % Check if at least the first Master fight has been cleared
            If *GB_MAC00_DojoRank  >  00000002 % Rank 3 or higher
                %Tea Reward hasn't been claimed yet; suddly remind the player
                % This will get called when actually giving the items...
            Else
                % Second Ginger T requirement hasn't been met: Safe to give tea items 
                Set *Var1 .True
            EndIf
        % After second reward...
        Case < 3
            % Check if at least the Master 2 fight has been cleared
            If *GB_MAC00_DojoRank  >  00000003 % Rank 4 or higher
                %Tea Reward hasn't been claimed yet; suddly remind the player
                % This will get called when actually giving the items...
            Else
                % Third Ginger T requirement hasn't been met: Safe to give tea items 
                Set *Var1 .True
            EndIf
        Default
            % All tea rewards claimed: Safe to give tea items
            Set *Var1 .True
        EndSwitch
        % Now use *Var1 whenever tea items are considered to determine if they should actually be given 
            %(True = give; False = only give healing)

        %In Easy Mode just give items regardless of rank...
        If *GB_FileCodeMode == 4` % 4 = "Mercy" filename
            Call  ShowMessageAtScreenPos    ( 002F0410  160`  40` ) % It seems like you're having a hard time...
            %If the player has no items, play the message of pitty and give a set of items.
            % Give player the items (Use pitty gift to visually show a gift without showing every item recieved; For ShowGotItem use only, don't actually give Pitty Gift to player)
            Call ShowGotItem    ( .Item:PittyGift .False 00000002 )
            % Give items (more items than usual since this is the "easy mode")
            Call AddItem        ( .Item:SuperShroom 00000001 )
            Call AddItem        ( .Item:MapleSyrup 00000001 )
            Call AddItem        ( .Item:ShroomCake 00000001 )
            % Only give Tea items if all other tea items are claimed...
            If *Var1 == .True
                Call AddItem        ( .Item:TastyTonic 00000001 )
                Call AddItem        ( .Item:VoltShroom 00000001 )
            % Otherwise play message to encourage getting the tea items
            Else
                Call  ShowMessageAtScreenPos    ( 002F0411  160`  40` ) % Have you explored around town?
            EndIf
            Return % End the script after items are given
        EndIf

        % Only apply these gifts to Master 2 & 3
        If *GB_MAC00_DojoRank > 2
            %If the player has no items, play the message of pitty and give a set of items.
            Call  ShowMessageAtScreenPos    ( 002F0410  160`  40` ) % It seems like you're having a hard time...
            % Give player the items (Use pitty gift to visually show a gift without showing every item recieved; For ShowGotItem use only, don't actually give Pitty Gift to player)
            Call ShowGotItem    ( .Item:PittyGift .False 00000002 )
            % Check if all tea items were claim so we can hint the player to get them if they haven't already....
            If *Var1 == .False
                Call  ShowMessageAtScreenPos    ( 002F0411  160`  40` ) % Have you explored around town?
            EndIf
            % Run a check on the file type and give items fitting of that challenge type....
            Switch *GB_FileCodeMode
                Case == 1` % For Danger Code (Low HP so give items accordingly)
                    Call AddItem        ( .Item:CocoaBean 00000001 )
                    Call AddItem        ( .Item:MapleSyrup 00000001 )
                    % Only give Tea items if all other tea items are claimed...
                    If *Var1 == .True
                        Call AddItem        ( .Item:TastyTonic 00000001 )
                        Call AddItem        ( .Item:VoltShroom 00000001 )
                    EndIf
                Case == 2` % For Tea Time Code (Only give Tea items if not already claimed)
                    % Only give Tea items if all other tea items are claimed...
                    If *Var1 == .True
                        Call AddItem        ( .Item:TastyTonic 00000001 )
                        Call AddItem        ( .Item:VoltShroom 00000001 )
                        Call AddItem        ( .Item:PleaseComeBack 00000001 )
                    EndIf
                Case == 7` % For Berserker mode (Give a life shroom since that's the only item they can use if actually using the badge)
                    Call AddItem        ( .Item:LifeShroom 00000001 )
                %Case == 8` % Everything but the Kitchen Sink is already impossibly hard so I'm consdiering adding items out of pitty later
                    %But it runs the no gift item flag so I'd have to hard code an expection elsewhere anyways
                %Case == 9` % For Super Challenge Mode (MegaHard) 
                    %But do I want to give items or is probably managing the few you get through all battles "part of the challenge?"
                Case == 10` % For Super Tea Mode (SuperTea) Same as Tea mode but without Halt Tea
                    If *Var1 == .True
                        Call AddItem        ( .Item:TastyTonic 00000001 )
                        Call AddItem        ( .Item:VoltShroom 00000001 )
                    EndIf
                Default % By default give 1 Super Shroom, 1 Maple Syrup, and tea items (if said items were already claimed)
                    Call AddItem        ( .Item:SuperShroom 00000001 )
                    Call AddItem        ( .Item:MapleSyrup 00000001 )
                    % Only give Tea items if all other tea items are claimed...
                    If *Var1 == .True
                        Call AddItem        ( .Item:TastyTonic 00000001 )
                        Call AddItem        ( .Item:VoltShroom 00000001 )
                    EndIf
            EndSwitch
        EndIf

    EndIf
    Return
    End

}

/%
% Tea check now happens inside the Pitty Item script... if I can make a script return a bool though it might be cleaner to put that in here later...
% Run some checks to assure we don't give Tea items if they already have T items to claim
#new:Script $GingerItemCheck
{

    Return
    End
}
%/